buildscript {
    repositories { maven { url "https://plugins.gradle.org/m2/" } }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.10'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
        classpath 'net.researchgate:gradle-release:2.6.0'
        classpath 'jp.classmethod.aws:gradle-aws-plugin:0.35'
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'jp.classmethod.aws.cloudformation' version '0.35'
    id 'net.researchgate.release' version '2.6.0'
    id 'org.asciidoctor.convert' version '1.5.7'
    id 'org.ajoberstar.grgit' version '2.1.1'
    id 'org.kordamp.gradle.livereload' version '0.2.1'
}

apply plugin: 'org.kordamp.gradle.livereload'

group = 'com.cfnstacks.gradle'
description = 'ArtifactS3 Gradle plugin'

asciidoctor {
    backends 'html5', 'pdf'
    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'stylesheet': 'stylesheets/style.css',
        'endpoint-url': 'https://cfn-stacks.com',
        'source-highlighter': 'coderay',
        'imagesdir': 'images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': '',
        'projectVersion': project.version,
        'gitHash': grgit.head().id,
        'gitHashShort': grgit.head().abbreviatedId
}

asciidoctorj {
    version = '1.5.6'
}

dependencies {
    compile gradleApi()
    compile 'com.amazonaws:aws-java-sdk:1.11.290'
    compile 'jp.classmethod.aws:gradle-aws-plugin:0.35'
    compile 'net.researchgate:gradle-release:2.6.0'
    compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.6'
    compile 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.16'
    compile 'org.kordamp.gradle:livereload-gradle-plugin:0.2.1'
    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'
}

liveReload {
    docRoot 'build/asciidoc/html5'
}

pluginBundle {
    website = 'https://cfn-stacks.com'
    vcsUrl = 'https://github.com/cfn-stacks/artifacts3-plugin'
    description = 'Plugin to publish to an ArtifactS3 repo'
    tags = ['artifacts', 'maven-publish', 's3']

    plugins {
        artifacts3Plugin {
            id = 'com.cfnstacks.gradle.artifacts3'
            displayName = 'ArtifactS3 Repo Publishing'
        }
    }
}

release { tagTemplate = 'v${version}' }
afterReleaseBuild.dependsOn publishPlugins
publish.dependsOn build

repositories {
    jcenter()
}

task docs(dependsOn: [asciidoctor]) {}
task projectVersion() { doLast { println project.version } }